# Produced by CVXGEN, 2019-05-05 13:54:30 -0400.
# CVXGEN is Copyright (C) 2006-2017 Jacob Mattingley, jem@cvxgen.com.
# The code in this file is Copyright (C) 2006-2017 Jacob Mattingley.
# CVXGEN, or solvers produced by CVXGEN, cannot be used for commercial
# applications without prior written permission from Jacob Mattingley.

# Filename: description.cvxgen.
# Description: A description of the CVXGEN problem.

dimensions
  m = 2  # inputs.
  n = 3  # states.
  T = 10  # horizon.
end

parameters
  A (n,n)  # dynamics matrix.
  B (n,m)  # transfer matrix.
  Q (n,n) psd  # state cost.
  Q_final (n,n) psd  # final state cost.
  R (m,m) psd  # input cost.
  x[0] (n)  # initial state.
  r[t] (n), t=0..T+1 # reference
  u_prev (m)
  u_max (m)  nonnegative  # amplitude limit.
  deltau_max (m) nonnegative  # slew rate limit.
end

variables
  x[t] (n), t=1..T+1  # state.
  u[t] (m), t=0..T  # input.
end

minimize
  sum[t=1..T](quad(x[t]-r[t], Q) + quad(u[t]-u[t-1], R)) + quad(x[T+1]-r[T+1], Q_final) + quad(x[0]-r[0],Q) + quad(u[0] - u_prev)
subject to
  x[t+1] == A*x[t] + B*u[t], t=0..T  # dynamics constraints.
  u[t][1] <= u_max[1], t=0..T  # maximum input box constraint.
  u[t][1] >= 0, t=0..T
  abs(u[t][2]) <= u_max[2], t=0..T  # maximum input box constraint.
  abs(u[t][1] - u[t-1][1]) <= deltau_max[1], t=1..T  # slew rate constraint.
  abs(u[0][1] - u_prev[1]) <= deltau_max[1]
  abs(u[t][2] - u[t-1][2]) <= deltau_max[2], t=1..T #delta theta
  abs(u[0][2] - u_prev[2]) <= deltau_max[2]
end
